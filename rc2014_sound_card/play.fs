( RETRIEVE ADDR OF CHANNEL INFO  )
: GETADDR ( N -- ADDR )
 2 * C000 + @
 1+
;

( DEREFERENCE STRING READ WITH 'WORD' )
: COUNT ( PAD -- PAD+1 LEN )
 DUP 1+ SWAP C@
;

( DEFINE NEW MUSIC WORD )
DEFINER PLAY ( -- )
 0 ( START WITH CHANNEL 0 )
 BEGIN
  20 WORD COUNT ( FIND AND DEREFERENCE NEXT STRING )
  DUP C, ( STORE LENGTH IN PARAM FIELD )
  OVER + SWAP ( WORK OUT LIMITS FOR LOOP )
  DO
   I C@ C, ( RETRIEVE EACH CHARACTER AND ADD TO PARAM FIELD )
  LOOP
  1+ ( NEXT CHANNEL )
  DUP 3 = ( CHECK IF DONE )
 UNTIL
 DROP ( BALANCE STACK )
DOES>
 0 ( START WITH CHANNEL 0 )
 BEGIN
  >R ( STORE CHANNEL NUMBER )
  DUP 1+ ( FIND START OF STRING )
  SWAP C@ ( RETRIEVE LENGTH OF STRING )
  OVER OVER + ( CALCULATE END OF STRING )
  >R ( STORE END OF STRING )
  OVER + ( CALCULATE END OF STRING )
  I' GETADDR 4 + ( CALC POINTER INTO CHANNEL INFO )
  ! ( SAVE END OF STRING )
  I' GETADDR ( CALC POINTER INTO CHANNEL INFO )
  ! ( SAVE START OF STRING )
  R> ( RETRIEVE END OF STRING )
  R> ( RETRIEVE CHANNEL NUMBER )
  1+ ( NEXT CHANNEL )
  DUP 3 = ( CHECK IF DONE )
 UNTIL
 DROP DROP ( BALANCE STACK )
 C006 CALL ( CALL PLAY ROUTINE )
;
